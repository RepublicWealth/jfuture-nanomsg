plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.4"
}

group 'au.com.republicwealth.nanomsg'
version '0.4.3-1'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
}

dependencies{
    compile "net.java.dev.jna:jna:${jnaVersion}"

    testCompile "junit:junit:4.11"
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

// custom tasks for creating source/test jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

apply from: 'gradle/bintray.gradle'